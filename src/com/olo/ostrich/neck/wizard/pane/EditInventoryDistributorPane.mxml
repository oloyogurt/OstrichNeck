<?xml version="1.0" encoding="utf-8"?>
<DefaultWizardCanvasPane paneTitle="Inventory Distributor" paneName="{PANE_NAME}"
	xmlns="com.olo.ostrich.neck.wizard.pane.*" xmlns:mx="http://www.adobe.com/2006/mxml" width="475" height="368">
	
	<mx:Script>
		<![CDATA[
			import mx.collections.Sort;
			import mx.collections.SortField;
			import com.olo.ostrich.neck.dto.Distributor;
			import com.olo.ostrich.neck.dto.InventoryItem;
			import mx.collections.ArrayCollection;
			
			static public const PANE_NAME:String = "editInventoryDistributorPane";
			
			[Bindable] private var _distributors:ArrayCollection = null;
			[Bindable] private var _inventory:InventoryItem = null;
			[Bindable] private var _iSelected:int = -1;
			
			
			override public function set auxProvider(ap:Object):void
			{
				if (!(ap is ArrayCollection)) throw new Error("ERROR: EditInventoryDistributorPane:set auxProvider only accepts an array of Distributor objects.");
				
				_distributors = ArrayCollection(ap);
				
				if (_inventory && _inventory.distributor)
				{
					setDistribSelection();
				}
				
				if (_distributors.sort == null)
				{
					var sf:SortField = new SortField();
					sf.name = "name";
					var sort:Sort = new Sort();
					sort.fields = [sf];
					_distributors.sort = sort;
					_distributors.refresh();
				}
			}
			
			
			override public function get auxProvider():Object
			{
				return _distributors;
			}
			
			
			override public function set dataProvider(dp:Object):void
			{
				if (dp != null)
				{
					if (!(dp is InventoryItem)) throw new Error("EditInventoryDistributorPane:set dataProvider - Only accepts an InventoryItem type");

					_inventory = InventoryItem(dp);
					
					if (_distributors && (_distributors.length > 0))
					{
						setDistribSelection();
					}
				}
			}
			
			
			private function setDistribSelection():void
			{
				for (var i:int=0; i < _distributors.length; ++i)
				{
					if (_inventory.distributor.id == Distributor(_distributors[i]).id)
					{
						_iSelected = i;
						onChange();
						break;
					}
				}
			}
			
			
			override public function get dataProvider():Object
			{
				return _inventory;
			}
			
			
			private function onChange():void
			{
				super.isValid = (distribGrid.selectedItem) ? true : false;
				
				_inventory.distributor = Distributor(distribGrid.selectedItem);
			}
			
			
			private function onSearch():void
			{
				var pattern:RegExp = new RegExp(searchTI.text, "i");
				for (var i:int=0; i < _distributors.length; ++i)
				{
					var distributor:Distributor = Distributor(_distributors.getItemAt(i));
					if (distributor.name.match(pattern))
					{
						distribGrid.selectedIndex = i;
						distribGrid.scrollToIndex(i);
						break;
					}
				}
				
				onChange();
			}
			
		]]>
	</mx:Script>
	
	<mx:AdvancedDataGrid id="distribGrid" change="onChange()" selectedIndex="{_iSelected}" dataProvider="{_distributors}" designViewDataType="flat" left="10" right="10" top="36" bottom="40">
		<mx:columns>
			<mx:AdvancedDataGridColumn headerText="Name" dataField="name"/>
			<mx:AdvancedDataGridColumn headerText="Transport" dataField="transportString" width="100"/>
		</mx:columns>
	</mx:AdvancedDataGrid>
	<mx:Label text="Search:" textAlign="right" fontWeight="bold" left="10" bottom="10"/>
	<mx:TextInput id="searchTI" change="onSearch()" left="66" bottom="10" right="10"/>
	<mx:Label text="Choose a Distributor" left="10" top="10" fontWeight="bold"/>
	
</DefaultWizardCanvasPane>
